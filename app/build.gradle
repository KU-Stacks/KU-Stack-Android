plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.gms.oss-licenses-plugin'
}

def keystorePropertiesFile = rootProject.file("app/signing/keystore.properties")
def localPropertiesFile = rootProject.file("local.properties")

def composeCompilerVersion = "1.4.7"

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    compileSdk 33

    defaultConfig {
        applicationId "com.ku_stacks.ku_ring"
        minSdk 24
        targetSdk 31
        versionCode 25
        versionName "1.3.2"

        def localProperties = new Properties()
        localProperties.load(new FileInputStream(localPropertiesFile))

        buildConfigField "String", "APPS_FLYER_DEV_KEY", localProperties['APPS_FLYER_DEV_KEY']
        buildConfigField "String", "SENDBIRD_APP_ID", localProperties['SENDBIRD_APP_ID']
        buildConfigField "String", "SENDBIRD_API_TOKEN", localProperties['SENDBIRD_API_TOKEN']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    viewBinding {
        enabled = true
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            manifestPlaceholders = [
                    appName : "@string/app_name_debug",
                    appIcon : "@drawable/ic_ku_ring_launcher_dev"
            ]
            buildConfigField "String", "API_BASE_URL", "\"https://dev.ku-ring.com/api/\""
            buildConfigField "String", "KURING_CAMPUS_OPEN_CHANNEL_URL", "\"kuring_main_anonymous\""

            applicationIdSuffix '.debug'
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            manifestPlaceholders = [
                    appName : "@string/app_name",
                    appIcon : "@drawable/ic_kuring_launcher"
            ]
            buildConfigField "String", "API_BASE_URL", "\"https://kuring.herokuapp.com/api/\""
            buildConfigField "String", "KURING_CAMPUS_OPEN_CHANNEL_URL", "\"kuring_campus_main_anonymous\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = composeCompilerVersion
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'com.google.firebase:firebase-messaging-ktx:21.0.1'

    // dagger hilt
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'com.google.dagger:hilt-android:2.45'
    kapt 'com.google.dagger:hilt-compiler:2.45'
    androidTestImplementation  'com.google.dagger:hilt-android-testing:2.45'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.45'
    testImplementation 'com.google.dagger:hilt-android-testing:2.45'
    kaptTest 'com.google.dagger:hilt-compiler:2.45'

    // start up
    implementation "androidx.startup:startup-runtime:1.1.0"

    // retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"

    // okHttp
    def okHttp_version = "4.9.0"
    implementation "com.squareup.okhttp3:okhttp:$okHttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttp_version"

    // coroutine
    def coroutine_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$coroutine_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"

    // rxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.7'

    // viewModel
    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation "androidx.activity:activity-ktx:1.2.3"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // viewPager2
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // fragment
    def fragment_version = '1.5.0'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"

    // firebase
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // open source notices
    implementation("com.google.android.gms:play-services-oss-licenses:17.0.0")

    // soft keyboard listener
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'

    // unit test
    androidTestImplementation("org.mockito:mockito-android:2.24.5")
    testImplementation 'org.mockito:mockito-inline:2.21.0'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.mockito:mockito-core:3.5.9'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:4.7"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"

    // paging3
    def paging_version = "3.0.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-common-ktx:$paging_version"
    implementation "androidx.paging:paging-rxjava3:$paging_version"

    // Room
    def room_version = "2.4.0-alpha03"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Compose
    def compose_bom = "2023.05.00"
    implementation platform("androidx.compose:compose-bom:$compose_bom")
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-test"
    implementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"

    implementation "androidx.activity:activity-compose:1.7.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.paging:paging-compose:1.0.0-alpha19"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"


    // Shimmer effect
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // tag library
    implementation 'com.github.yeon-kyu:AndroidTagView:v1.1.7-alpha1'

    // WebSocket
    implementation "org.java-websocket:Java-WebSocket:1.5.1"

    // HoldableSwipeHandler
    implementation 'com.github.yeon-kyu.HoldableSwipeHandler:HoldableSwipeHandler:1.2.2'

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    // swipeRefreshLayout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

    // appsFlyer
    implementation 'com.appsflyer:af-android-sdk:6.3.2'

    // indicator
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'

    // sendbird sdk
    implementation 'com.sendbird.sdk:sendbird-chat:4.0.0-beta.2'

    // google play-auth
    implementation 'com.google.android.gms:play-services-auth:20.3.0'

    // google AdMob
    implementation 'com.google.android.gms:play-services-ads:21.2.0'
}

kapt {
    correctErrorTypes true
}
