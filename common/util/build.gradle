plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

def localPropertiesFile = rootProject.file("local.properties")

android {
    namespace 'com.ku_stacks.ku_ring.util'
    compileSdk 33

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        def localProperties = new Properties()
        localProperties.load(new FileInputStream(localPropertiesFile))

        buildConfigField "String", "APPS_FLYER_DEV_KEY", localProperties['APPS_FLYER_DEV_KEY']
        buildConfigField "String", "SENDBIRD_APP_ID", localProperties['SENDBIRD_APP_ID']
        buildConfigField "String", "SENDBIRD_API_TOKEN", localProperties['SENDBIRD_API_TOKEN']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "API_BASE_URL", "\"https://kuring.herokuapp.com/api/\""
            buildConfigField "String", "KURING_CAMPUS_OPEN_CHANNEL_URL", "\"kuring_campus_main_anonymous\""
        }
        debug {
            buildConfigField "String", "API_BASE_URL", "\"http://13.113.178.212:8080/api/\""
            buildConfigField "String", "KURING_CAMPUS_OPEN_CHANNEL_URL", "\"kuring_main_anonymous\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation libs.androidx.core.ktx

    // coroutines
    implementation libs.bundles.coroutines
    testImplementation libs.kotlinx.coroutines.test

    // dagger hilt
    kapt libs.androidx.hilt.compiler
    implementation libs.hilt.android
    kapt libs.hilt.compiler
    androidTestImplementation  libs.hilt.android.testing
    kaptAndroidTest libs.hilt.compiler
    testImplementation libs.hilt.android.testing
    kaptTest libs.hilt.compiler

    // retrofit
    implementation libs.bundles.retrofit

    // okHttp
    implementation libs.okhttp
    implementation libs.okhttp.logging.interceptor
    implementation libs.okhttp.mockwebserver

    // Timber
    implementation libs.timber

    // tests
    testImplementation libs.bundles.unit.test
}